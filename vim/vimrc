" ==================================================================
" For Vim, copy it to
"     for Unix and OS/2:  ~/.vimrc
"  for MS-DOS and Win32:  $VIM\_vimrc
"
" For NeoVim
" - Use `$XDG_CONFIG_HOME/nvim/init.vim` instead of `.vimrc` for your |config|.
" - Use `$XDG_CONFIG_HOME/nvim` instead of `.vim` to store configuration files.
"
" help :h 'setting'
" check :set xxx?
" reverse :set xxx!
" ==================================================================

" ------------------------------------------------------------------
" Starting
" ------------------------------------------------------------------

" Use Vim settings, rather than Vi settings (much better!).
" This must be first, because it changes other options as a side effect.
set nocompatible

" Switch syntax highlighting on.
syntax on

" Enable file type detection, plugin and language-dependent indenting.
filetype plugin indent on

if has("gui_running")
    set t_Co=256
    let g:molokai_original=1
    colorscheme molokai

    " guifont
    if has("win32")
        set guifont=Fixedsys\ Console:h11
    else
        set guifont=MiscFixed\ 11
    endif

    " Maximize window when enter vim
    if has("win32")
        autocmd GUIEnter * simalt ~x
    else
        " TODO: no way right now
    endif

else " if we are in terminal mode
    set t_Co=8
    colorscheme moonlight
endif

" When editing a file, always jump to the last known cursor position.
" Don't do it when the position is invalid or when inside an event handler
" (happens when dropping a file on gvim).
autocmd BufReadPost *
\ if line("'\"") > 0 && line("'\"") <= line("$") |
\   exe "normal g`\"" |
\ endif

" hanlde file ext md.html
autocmd BufNewFile,BufRead *.md.html set ft=markdown

" ------------------------------------------------------------------
" Options
" ------------------------------------------------------------------
set backspace=indent,eol,start " Make backspace behave in a sane manner, allow backspacing over everything in insert mode.

set belloff=all

set autoindent
set smartindent

set shiftwidth=4 " set indent to 4 spaces
set tabstop=4 " set tabstop to 4 spaces
set softtabstop=4 " set softtabstop to 4 spaces
set expandtab " set expandtab on, the tab will be change to space automaticaly
set smarttab
set shiftround

set clipboard=unnamed,unnamedplus " use clipboard register '*'(unnamed) and '+'(unnamedplus) for all y, d, c, p ops

set guioptions+=b " Present the bottom scrollbar when the longest visible line exceen the window
set guioptions-=m " disable menu
set guioptions-=T " disable toolbar

set langmenu=none " always use English menu

set autoread " auto read same-file change

set showmatch " show matching paren, actually shows
set matchtime=0 " 0 second to show the matching paren ( much faster )
set number " show lineNumber
set scrolloff=5 " keep cursor away from edges of screen
set sidescrolloff=10
set sidescroll=1

set nowrap " I don't like wrap, cause in split window mode, it feel strange

set wildmenu " turn on wild menu, try typing :h and press <Tab>
set wildignore+=*.a,*.o
set wildignore+=*.bmp,*.gif,*.ico,*.jpg,*.png
set wildignore+=.DS_Store,.git,.hg,.svn
set wildignore+=*~,*.swp,*.tmp

set ruler " show the cursor position all the time
set showcmd " display incomplete commands
set showmode " display edit mode
set cmdheight=1 " 1 screen lines to use for the command-line
set hidden " allow to change buffer without saving
set shortmess=atIO " shortens messages to avoid 'press a key' prompt
set lazyredraw " do not redraw while executing macros (much faster)
set display+=lastline " for easy browse last line with wrap text

set showfulltag " show tag with function protype.

set hlsearch " highlight search
set incsearch " do incremental searching
set ignorecase " Set search/replace pattern to ignore case
set smartcase " Set smartcase mode on, If there is upper case character in the search patern, the 'ignorecase' option will be override.

set history=50 " keep 50 lines of command line history
set updatetime=1000 " default = 4000
set nobackup " make no backup file and leave it around
set noswapfile " no swap file

set statusline=[%F]%h%w%r%m%=[%{strlen(&ft)?&ft:'none'},%{(&fenc==\"\"?&enc:&fenc)},%{&fileformat}][Line:%l/%L,Column:%c][%p%%]
set laststatus=2

set whichwrap=b,s,<,>,[,]
set formatoptions=mB

set fileformats=unix,dos,mac " auto detect file formats

set encoding=utf-8
set fileencodings=ucs-bom,utf-8,chinese,japanese " auto detect file encodings

set complete=.,w,b,u,i " default is '.,w,b,u,t,i', tags match is not from the word start, disable it
set completeopt=menu

set nrformats= " increase/decrease number format to decimal (default is octal)
set virtualedit=block " In Visual Block Mode, cursor can be positioned where there is no actual character
set mouse=a " use mouse
set nomousehide " do not hide mouse

set diffopt=filler,vertical,foldcolumn:1

set autowrite " auto write when make...

set nojoinspaces " join line with one space after .

set ttyfast

set nostartofline " keep column position when G etc.

" ------------------------------------------------------------------
" Functions
" ------------------------------------------------------------------

" used by alias definition
func EatChar(pat)
   let c = nr2char(getchar(0))
   return (c =~ a:pat) ? '' : c
endf

" number every line
func LineIt()
    exe "s/^/".line(".")."/"
endf

" reverse all lines
func ReverseIt()
    exe "g/^/m0"
endf

" toggle fold state between closed and opened.
func ToggleFold()
    if foldlevel('.') == 0
        "normal! l
    else
        if foldclosed('.') < 0
            . foldclose
        else
            . foldopen
        endif
    endif
    " Clear status line
    echo
endf

" show current highlight group
func SyntaxItem()
  echo synIDattr(synID(line("."), col("."), 1), "name")
endf

" build from current dir
"let &makeprg = '[[ -f Makefile ]] && make || make -C ..'
func Build()
    "exe "cd %:h"
    silent exe "make"
    silent exe "cw"
    silent exe "cc"
    exe "redraw!"
endf

" toggle window size
func ToggleWinSize(vert)
    if a:vert
        if winwidth(0) < 50
            exe "vertical res" . winwidth(0)*2
        else
            exe "vertical res" . winwidth(0)/2
        endif
    else
        if winheight(0) < 40
            exe "res" . winheight(0)*2
        else
            exe "res" . winheight(0)/2
        endif
    endif
endf

" AutoClose, could be overruled
func AutoCloseOn()
    inoremap ( ()<ESC>:let leavechar=")"<CR>i
    inoremap { {}<ESC>:let leavechar="}"<CR>i
    inoremap [ []<ESC>:let leavechar="]"<CR>i
    inoremap < <><ESC>:let leavechar=">"<CR>i
    inoremap " ""<ESC>:let leavechar="\""<CR>i
    inoremap ' ''<ESC>:let leavechar="'"<CR>i
endf

func AutoCloseOff()
    iunmap (
    iunmap {
    iunmap [
    iunmap <
    iunmap "
    iunmap '
endf

call AutoCloseOn()
" get out
inoremap <unique> <C-j> <ESC>:exec "normal f" . leavechar<CR>a

" ------------------------------------------------------------------
" Mappings
" ------------------------------------------------------------------

" wean yourself off the arrow keys!
noremap <unique> <Up> <nop>
noremap <unique> <Down> <nop>
noremap <unique> <Left> <nop>
noremap <unique> <Right> <nop>

" move quickly
nnoremap <unique> <SPACE> 20j
nnoremap <unique> <BS> 20k

" win-like select all
nnoremap <unique> <LEADER>a ggvG$

" Yanking
nnoremap Y y$

" clear highlight
nnoremap <C-L> <Cmd>nohlsearch<Bar>diffupdate<Bar>normal! <C-L><CR>

" Edit another file in the same directory as the current file
if has("unix")
    noremap <unique> <Leader>e :e <C-R>=expand("%:p:h") . "/" <CR>
else
    noremap <unique> <Leader>e :e <C-R>=expand("%:p:h") . "\\" <CR>
endif

" switch window
noremap <unique> <silent> <LEADER>w <C-w>p
" switch buffer
noremap <unique> <silent> <LEADER><TAB> :b#<CR>
noremap <unique> <silent> <C-j> :bn<CR>
noremap <unique> <silent> <C-k> :bp<CR>

" swap with the next word
noremap <unique> <silent> <LEADER>sw "_yiw:s/\(\%#\w\+\)\(\W\+\)\(\w\+\)/\3\2\1/<cr><c-o>

" Enhance '<' '>' , do not need to reselect the block after shift it.
vnoremap <unique> < <gv
vnoremap <unique> > >gv

" turn off diff and set nowrap back (should op on to be quited window)
noremap <unique> <silent> <LEADER>d :diffoff!<CR>:bd<CR>:set nowrap<CR>

" easy search tags
noremap <unique> <silent> <LEADER>] g<C-]>

noremap <unique> <silent> <LEADER>f :call ToggleFold()<CR>

noremap <unique> <F9> :call Build()<CR>

" search current word with Ag in same directory
"noremap <unique> <Leader>ag :Ag! -Uw <C-R>=expand("<cword>")<CR> <C-R>=expand("%:p:h")<CR> <CR>

" ------------------------------------------------------------------
" Highlights
" ------------------------------------------------------------------

hi Folded ctermfg=gray guibg=gray
"hi FoldColumn ctermfg=gray guibg=gray

" highlight trailing spaces
hi ExtraWhitespace ctermbg=red guibg=red
autocmd BufWinEnter * match ExtraWhitespace /\s\+$/
autocmd InsertLeave * match ExtraWhitespace /\s\+$/
autocmd InsertEnter * match ExtraWhitespace /\s\+\%#\@<!$/


" ------------------------------------------------------------------
" Plugins
" ------------------------------------------------------------------

" nerdtree
noremap <unique> <silent> <F4> :NERDTreeToggle<CR>
noremap <unique> <silent> <LEADER>nf :NERDTreeFind<CR>
let NERDTreeIgnore = ['\.a$', '\.o$', '\.so$', '\.lo$', '\.la$', '\.pyc$', '\.d$', '\.gcno$', '\.gcda$']
let NERDTreeMinimalUI = 1
let NERDTreeDirArrows = 0
hi def link NERDTreeRO Normal

" tagbar
noremap <unique> <silent> <F5> :TagbarToggle<CR>

" ctrlp
noremap <unique> <silent> <F6> :CtrlP<CR>
noremap <unique> <silent> <F8> :CtrlPBuffer<CR>
let g:ctrlp_by_filename = 1
let g:ctrlp_max_height = 20
let g:ctrlp_custom_ignore = {
    \ 'dir':  '\v[\/]\.(git|hg|svn)$',
    \ 'file': '\v\.(exe|dll|a|o|so|lo|la|pyc|d)$',
    \ 'link': 'SOME_BAD_SYMBOLIC_LINKS' }
"   \ 'file': '\v(\.cpp|\.h|\.hh|\.cxx)@<!$'

" quickhl
nmap <unique> <silent> <LEADER>h <Plug>(quickhl-toggle)
nnoremap <unique> <silent> <LEADER>l ml:execute 'match Search /\%'.line('.').'l/'<CR>
nmap <unique> <silent> <C-h> :match<CR>:nohlsearch<CR><Plug>(quickhl-reset)

" sketch
"noremap <unique> <F7> :call ToggleSketch()<CR>

" plugin windows mapping
" <space> to toggle window size, <q> to quit
" see help xterm-cursor-keys etc.
autocmd BufNewFile NERD_tree_1 noremap <buffer> <silent> <SPACE> :call ToggleWinSize(1)<CR>0
autocmd BufNewFile __Tagbar__ noremap <buffer> <silent> <LEADER><SPACE> :call ToggleWinSize(1)<CR>0
autocmd BufReadPost quickfix
\ noremap <buffer> <silent> q <C-w>p:ccl<CR>|
\ noremap <buffer> <silent> <SPACE> :call ToggleWinSize(0)<CR>

" numbers
let g:numbers_exclude = ['tagbar', 'nerdtree']

" supertab
let g:SuperTabDefaultCompletionType = '<c-n>'

" expand region
vmap v <Plug>(expand_region_expand)
vmap <C-v> <Plug>(expand_region_shrink)

" sneak 'ignorecase' and 'smartcase'
let g:sneak#use_ic_scs = 1

" vim-go
autocmd FileType go nnoremap <leader>gr :GoReferrers<CR>
autocmd FileType go nnoremap <leader>i :GoInfo<CR>
autocmd Filetype go command! -bang A call go#alternate#Switch(<bang>0, 'edit')
noremap <unique> <silent> <F7> :GoDecls<CR>
let g:go_fmt_command = "goreturns"
let g:go_fmt_autosave = 1
let g:go_list_type = "quickfix"
let g:go_textobj_include_function_doc = 1
let g:go_highlight_types = 1
let g:go_metalinter_enabled = ['vet', 'golint', 'errcheck']
let g:go_metalinter_autosave = 0
let g:go_auto_sameids = 0
let g:go_auto_type_info = 0
